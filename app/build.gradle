import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

def releaseTime() {
    def df = new SimpleDateFormat('yyyyMMddHHmm')
    df.setTimeZone(TimeZone.getDefault())
    return df.format(new Date())
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.ymsfd.practices"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionMajor
        versionName "$rootProject.ext.versionMajor.$rootProject.ext.versionMinor.$rootProject.ext.versionPatch"
    }

    signingConfigs {
        release {
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
            keyAlias WOODENTEA_ALIAS
            keyPassword WOODENTEA_PASSWORD
        }

        debug {
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
            keyAlias WOODENTEA_ALIAS
            keyPassword WOODENTEA_PASSWORD
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "API_URL", '"http://api.dev.com/"'
            resValue "string", "app_name", "Refill"
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }

        release {
            buildConfigField "String", "API_URL", '"http://api.prod.com/"'
            resValue "string", "app_name", "Nib"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        Official {
            manifestPlaceholders = [CHANNEL_VALUE: "Official"]
        }
        wandoujia {
            manifestPlaceholders = [CHANNEL_VALUE: "wandoujia"]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            File outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                String fileName;
                if (variant.buildType.name.equals('debug')) {
                    fileName = "${defaultConfig.applicationId}_" +
                            "v${defaultConfig.versionName}_" +
                            "${variant.productFlavors.collect()[0].name}.apk"
                } else {
                    fileName = "${defaultConfig.applicationId}_" +
                            "v${defaultConfig.versionName}_${releaseTime()}_" +
                            "${variant.productFlavors.collect()[0].name}.apk"
                }
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':zxing')
    compile "com.android.support:design:$rootProject.ext.supportVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportVersion"
    compile "com.android.support:cardview-v7:$rootProject.ext.supportVersion"
    compile "com.android.support:support-v4:$rootProject.ext.supportVersion"
    compile "com.squareup.okhttp3:okhttp:$rootProject.ext.okHttp"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okHttp"
    compile "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofit"
    compile "com.squareup.retrofit2:adapter-rxjava:$rootProject.ext.retrofit"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofit"

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.facebook.stetho:stetho-okhttp3:1.3.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.5.4'
}
