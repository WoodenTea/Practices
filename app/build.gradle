import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

def releaseTime() {
    def df = new SimpleDateFormat('yyyyMMddHHmm')
    df.setTimeZone(TimeZone.getDefault())
    return df.format(new Date())
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "com.ymsfd.practices"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    signingConfigs {
        release {
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
            keyAlias WOODENTEA_ALIAS
            keyPassword WOODENTEA_PASSWORD
        }

        debug {
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
            keyAlias WOODENTEA_ALIAS
            keyPassword WOODENTEA_PASSWORD
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "API_URL", '"http://api.dev.com/"'
            resValue "string", "app_name", "Refill"
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }

        release {
            buildConfigField "String", "API_URL", '"http://api.prod.com/"'
            resValue "string", "app_name", "Nib"

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                File outputFile = output.outputFile
                if (variant.buildType.name.equals("release") && outputFile != null
                        && outputFile.name.endsWith('.apk')) {
                    String fileName = "${defaultConfig.applicationId}_v" +
                            "${defaultConfig.versionName}_${releaseTime()}_" +
                            "${variant.productFlavors.get(0).name}.apk"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }

    productFlavors {
        Official {
            manifestPlaceholders = [CHANNEL_VALUE: "Official"]
        }
        wandoujia {
            manifestPlaceholders = [CHANNEL_VALUE: "wandoujia"]
        }
    }
}

def supportVersion = '23.3.0'
def okHttp = '3.2.0'
def retrofit = '2.0.1'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':zxing')
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:support-v4:$supportVersion"

    compile "com.squareup.okhttp3:okhttp:$okHttp"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttp"
    compile "com.squareup.retrofit2:retrofit:$retrofit"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit"
    compile "com.squareup.retrofit2:converter-gson:$retrofit"

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'io.reactivex:rxjava:1.1.2'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    compile 'com.trello:rxlifecycle:0.4.0'
    compile 'com.google.code.gson:gson:2.6.2'

    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.facebook.stetho:stetho-okhttp3:1.3.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.3.1'
}
