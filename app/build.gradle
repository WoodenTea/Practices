apply plugin: 'com.android.application'
apply plugin: 'findbugs'
apply plugin: 'org.greenrobot.greendao'

task findbugs(type: FindBugs) {
    ignoreFailures = true
    effort = "max"
    reportLevel = "low"
    excludeFilter = new File("${rootProject.rootDir}/quality/findbugs-filter.xml")
    classes = files("$project.buildDir/intermediates/classes")
    classpath = files()
    source = fileTree('src/main/java/')
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "$rootProject.buildDir/reports/findbugs/findbugs.xml"
        }
        html {
            destination "$rootProject.buildDir/reports/findbugs/findbugs.html"
        }
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.ymsfd.practices"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionMajor
        versionName "$rootProject.ext.versionMajor.$rootProject.ext.versionMinor.$rootProject.ext.versionPatch"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
            keyAlias WOODENTEA_ALIAS
            keyPassword WOODENTEA_PASSWORD
        }

        debug {
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
            keyAlias WOODENTEA_ALIAS
            keyPassword WOODENTEA_PASSWORD
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "API_URL", '"http://api.dev.com/"'
            resValue "string", "app_name", "Refill"
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }

        release {
            buildConfigField "String", "API_URL", '"http://api.prod.com/"'
            resValue "string", "app_name", "Nib"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        Official {
            manifestPlaceholders = [CHANNEL_VALUE: "Official"]
        }
        wandoujia {
            manifestPlaceholders = [CHANNEL_VALUE: "wandoujia"]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            File outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                String fileName;
                if (variant.buildType.name.equals('debug')) {
                    fileName = "${defaultConfig.applicationId}_" +
                            "v${defaultConfig.versionName}_" +
                            "${variant.productFlavors.collect()[0].name}.apk"
                } else {
                    fileName = "${defaultConfig.applicationId}_" +
                            "v${defaultConfig.versionName}_${rootProject.ext.releaseTime}_" +
                            "${variant.productFlavors.collect()[0].name}.apk"
                }
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':zxing')
    compile project(':swagpoint')

    compile "com.android.support:design:$rootProject.ext.supportVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportVersion"
    compile "com.android.support:cardview-v7:$rootProject.ext.supportVersion"

    compile "com.squareup.okhttp3:okhttp:$rootProject.ext.okHttp"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okHttp"
    compile "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofit"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofit"
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.markomilos:paginate:0.5.1'
    compile 'com.google.code.gson:gson:2.8.0'

    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile 'org.greenrobot:greendao:3.2.0'
}

greendao {
    schemaVersion 1
}
